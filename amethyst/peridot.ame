amethyst Peridot_parser{
	root = (body|defi|sequence)*:a .* {a}
	
	body = "class" "" name:name defi*:ary "end" @Klass
	
	defi = "def" "" defname:name '(' ( ')' break | .:args[])*  sequence:ary[] "end" @Def
	name = <a-zA-Z_>:s <a-zA-Z0-9_>*:{s+it*""}
	defname = (<^ \t\r\n()>)*:x {leterize(x*"")}
	
	atom = ""
       ( number:n -> CCode["Int(#{n})"]
			 | '(' expr:e ")" {e}
			 | 'if' "(" expr:expr ")" block:block {If[{:expr=>expr,:block=>block}]}
			 | '{' ('}' break | &'{' atom:{'{'+it[0]+'}'}:s[] | .:s[])* {CCode[s*""]}
			 | name:{leterize(it)}:name '(' args:ary[] ')' @Call
 		   | ~'end' name:name @Var
			 )

	args = expr
	method = name
	
	expr_postfixed = atom:a ( 
                          ( '[' args:arg "]" "=" expr:arg2        -> Call[{:name=>leterize("[]="),:ary=>[a,arg,arg2]}]
													| '[' args:arg "]"                      -> Call[{:name=>leterize("[]") ,:ary=>[a,arg]}]
													| '.' method):a
	                        )* {a}
               

  expr = "" name:name '=' expr:expr @Assign
			 | expr_ar1

	expr_ar1 = expr_ar2:a (("+"|"-"):op expr_ar2:b {Call[{:name=>leterize(op),:ary=>[a,b]}]}:a)* {a}
	expr_ar2 = expr_ar3:a (("*"|"/"):op expr_ar3:b {Call[{:name=>leterize(op),:ary=>[a,b]}]}:a)* {a}
	expr_ar3 = expr_postfixed:a     (("**"):op    expr_postfixed:b     {Call[{:name=>leterize(op),:ary=>[a,b]}]}:a)* {a}

	block = "{" sequence:s '}' {s}

	sequence = expr:ary[] ( newline expr:ary[])* @Seq
}
