amethyst Peridot_parser{
	root = defi*:a .* {a}
		
	defi = "def" "" name2:name '(' ( ')' break | .:args[])*  sequence:ary[] "end" @Def
	name2 = (<^ \t\r\n()>)*:x {leterize(x*"")}
	name = <a-zA-Z_>:s <a-zA-Z0-9_>*:{s+it*""}
	
	atom = ""
       ( number
			 | '(' expr:e ')' {e}
			 | 'if' "(" expr:expr ")" block:block {If[{:expr=>expr,:block=>block}]}
			 | '{' ('}' break | .:s[])* {CCode[s*""]}
			 | name:name '(' expr:ary[] ')' @Call
 		   | name:name &{name!="end"} @Var
			 )

  expr = "" name:name '=' expr:expr @Assign
			 | expr_ar1

	expr_ar1 = expr_ar2:a (("+"|"-"):op expr_ar2:b {Call[{:name=>leterize(op),:ary=>[a,b]}]}:a)* {a}
	expr_ar2 = expr_ar3:a (("*"|"/"):op expr_ar3:b {Call[{:name=>leterize(op),:ary=>[a,b]}]}:a)* {a}
	expr_ar3 = atom:a     (("**"):op    atom:b     {Call[{:name=>leterize(op),:ary=>[a,b]}]}:a)* {a}

	block = "{" sequence:s '}' {s}

	sequence = expr:ary[] ( newline expr:ary[])* @Seq
}
